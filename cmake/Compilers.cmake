if("${CMAKE_CXX_COMPILER_ID}" MATCHES "Clang$")
    set(COMPILER_CLANG ON)
    if(CMAKE_CXX_COMPILER_FRONTEND_VARIANT STREQUAL "MSVC")
        set(COMPILER_CLANG_MSVC ON)
        set(COMPILER_MSVC_LIKE ON)
    elseif(CMAKE_CXX_COMPILER_FRONTEND_VARIANT STREQUAL "GNU")
        set(COMPILER_CLANG_GNU ON)
    endif()
else()
    set(COMPILER_CLANG OFF)
endif()

if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
    set(COMPILER_GCC ON)
else()
    set(COMPILER_GCC OFF)
endif()

if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
    set(COMPILER_MSVC ON)
    set(COMPILER_MSVC_LIKE ON)
    set(COMPILER_CLANG_MSVC OFF)
else()
    set(COMPILER_MSVC OFF)
endif()

option(USE_TIDY "Run clang-tidy along with build")
if(USE_TIDY)
    set(CMAKE_CXX_CLANG_TIDY clang-tidy)

    option(USE_CLAZY "Enable clazy checks")
    if(USE_CLAZY)
        set(CLAZY_PLUGIN_PATH "ClazyClangTidy" CACHE STRING "Path to clazy plugin")
        execute_process(
            COMMAND "${CMAKE_CXX_CLANG_TIDY}" "--load=${CLAZY_PLUGIN_PATH}" "--version"
            ERROR_STRIP_TRAILING_WHITESPACE
            ERROR_VARIABLE RUN_TIDY_ERROR
            OUTPUT_QUIET
            RESULT_VARIABLE RUN_TIDY_RESULT)
        if(NOT RUN_TIDY_RESULT EQUAL 0)
            message(FATAL_ERROR "Error while running clang-tidy")
        endif()
        if(RUN_TIDY_ERROR)
            message(FATAL_ERROR "Invalid path to clazy plugin")
        endif()
        list(APPEND CMAKE_CXX_CLANG_TIDY "--load=${CLAZY_PLUGIN_PATH}")
    elseif(CLAZY_PLUGIN_PATH)
        message(FATAL_ERROR "Clazy plugin can be used only when USE_CLAZY=ON")
    endif()
elseif(USE_CLAZY)
    message(FATAL_ERROR "Clazy checks can be enabled only when USE_TIDY=ON")
endif()
